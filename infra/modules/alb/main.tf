resource "aws_lb" "ecs_alb" {
  name               = var.alb_name
  internal           = false
  load_balancer_type = "application"
  security_groups    = var.alb_security_group_ids
  subnets            = var.public_subnet_ids

  drop_invalid_header_fields = true

  tags = {
    Name = var.alb_name
  }
}

resource "aws_lb_target_group" "ecs_tg" {
  name        = "${var.alb_name}-tg"
  target_type = "ip"
  port        = var.target_group_port
  protocol    = "HTTP"
  vpc_id      = var.vpc_id

  health_check {
    path                = "/"
    protocol            = "HTTP"
    matcher             = "200-399"
    interval            = 30
    timeout             = 5
    healthy_threshold   = 2
    unhealthy_threshold = 2
  }

  tags = {
    Name = "${var.alb_name}-tg"
  }
}

resource "aws_lb_listener" "ecs_http" {
  load_balancer_arn = aws_lb.ecs_alb.arn
  port              = 80
  protocol          = "HTTP"

  default_action {
    type = "redirect"

    redirect {
      port        = "443"
      protocol    = "HTTPS"
      status_code = "HTTP_301"
    }
  }

  depends_on = [
    aws_lb.ecs_alb
  ]
}

resource "aws_lb_listener" "ecs_https" {
  load_balancer_arn = aws_lb.ecs_alb.arn
  port              = 443
  protocol          = "HTTPS"
  ssl_policy = "ELBSecurityPolicy-TLS13-1-2-2021-06"     
  certificate_arn   = var.certificate_arn

  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.ecs_tg.arn
  }

  depends_on = [
    aws_lb.ecs_alb,
    aws_lb_target_group.ecs_tg
  ]
}

resource "aws_security_group" "alb_sg" {
  name        = var.alb_sg_name
  description = "Security group for ALB allowing HTTP/HTTPS inbound traffic"
  vpc_id      = var.vpc_id

  tags = {
    Name = var.alb_sg_name
  }

}

resource "aws_security_group_rule" "allow_http_inbound" {
  description       = "Allow HTTP inbound traffic to ALB for redirect to HTTPS"
  type              = "ingress"
  from_port         = 80
  to_port           = 80
  protocol          = "tcp"
  cidr_blocks       = ["0.0.0.0/0"]
  security_group_id = aws_security_group.alb_sg.id
}

resource "aws_security_group_rule" "allow_https_inbound" {
  description       = "Allow HTTPS inbound traffic to ALB"
  type              = "ingress"
  from_port         = 443
  to_port           = 443
  protocol          = "tcp"
  cidr_blocks       = ["0.0.0.0/0"]
  security_group_id = aws_security_group.alb_sg.id
}

resource "aws_security_group_rule" "allow_all_traffic_outbound" {
  description       = "Allow outbound traffic from ALB to application targets"
  type              = "egress"
  from_port         = 0
  to_port           = 0
  protocol          = "-1"
  cidr_blocks       = ["0.0.0.0/0"]
  security_group_id = aws_security_group.alb_sg.id

}
